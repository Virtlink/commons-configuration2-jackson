plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.5"
}
apply from: 'properties.gradle'


sourceCompatibility = 1.7
targetCompatibility = 1.7


configurations {
    provided
}


repositories {
    mavenCentral()
}


dependencies {
    compile 'org.apache.commons:commons-configuration2:2.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.5'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.5' // upgrade 2.7.0 dependency of jackson-databind

    provided 'com.google.code.findbugs:findbugs:3.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.7.5'
    testCompile 'org.yaml:snakeyaml:1.17' // upgrade 1.15 dependency of jackson-dataformat-yaml
    testCompile 'com.google.guava:guava:18.0'
    testCompile 'commons-beanutils:commons-beanutils:1.9.2'
    testCompile 'commons-collections:commons-collections:3.2.2' // upgrade 3.2.1 dependency of commons-collections (http://www.kb.cert.org/vuls/id/576313)
}


sourceSets.main.compileClasspath += configurations.provided
idea.module.scopes.PROVIDED.plus += [configurations.provided]
eclipse.classpath.plusConfigurations += configurations.provided


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


def pomConfig = {
    scm {
        url scm_url
        connection scm_connection
        developerConnection scm_devconnection
    }
    licenses {
        license {
            name license_id
            url license_url
            distribution license_distribution
        }
    }
    developers {
        developer {
            name developer_name
            id developer_id
            email developer_email
        }
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode 'name', project.project_name
                root.appendNode 'description', project.project_description
                root.appendNode 'url', project.project_url
                root.appendNode 'inceptionYear', project.project_inceptionyear
                root.children().last() + pomConfig
            }
        }
    }
}


bintray {
	user = project.bintray_username
	key = project.bintray_apikey
	publications = ['mavenJava']
	publish = true
	pkg {
		name = project.name
		desc = project_description
		websiteUrl = project_url
		issueTrackerUrl = project_issues
		vcsUrl = vcs_url
		licenses = [license_name]
		repo = project.bintray_repo
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = project_description
			released  = new Date()
			vcsTag = vcs_tag
			gpg {
				sign = true
				passphrase = gpg_passphrase
			}
            mavenCentralSync {
                sync = true
                user = sonatype_username
                password = sonatype_password
            }
		}
	}
}

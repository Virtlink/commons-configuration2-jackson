plugins {
    id "java-library"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}
apply from: 'properties.gradle'


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}


dependencies {
    api         "org.apache.commons:commons-configuration2:$commonsConfigurationVersion"
    api         "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    compileOnly "com.google.code.findbugs:findbugs:$findbugsVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    testImplementation "com.google.guava:guava:$guavaVersion"
    testImplementation "commons-beanutils:commons-beanutils:$beanutilsVersion"
}

java {
    withJavadocJar()
    withSourcesJar()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.project_name
                description = project.project_description
                url = project.project_url
                inceptionYear = project.project_inceptionyear
                licenses {
                    license {
                        name = project.license_id
                        url = project.license_url
                        distribution = project.license_distribution
                    }
                }
                developers {
                    developer {
                        id = project.developer_id
                        name = project.developer_name
                        email = project.developer_email
                    }
                }
                scm {
                    connection = project.scm_connection
                    developerConnection = project.scm_devconnection
                    url = project.scm_url
                }
            }
        }
    }
}


bintray {
	user = project.bintray_username
	key = project.bintray_apikey
	publications = ['mavenJava']
	publish = true
	pkg {
		name = project.name
		desc = project_description
		websiteUrl = project_url
		issueTrackerUrl = project_issues
		vcsUrl = vcs_url
		licenses = [license_name]
		repo = project.bintray_repo
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = project_description
			released  = new Date()
			vcsTag = vcs_tag
			gpg {
				sign = true
				passphrase = gpg_passphrase
			}
            mavenCentralSync {
                sync = true
                user = sonatype_username
                password = sonatype_password
            }
		}
	}
}
